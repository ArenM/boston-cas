#= require ./namespace

class App.ServiceManager.Controller
  constructor: (@element) ->
    # These two pieces of data are used to determine the form_input_prefix
    @base_object_name = $(@element).data 'base-object-name'
    @selected_services = new App.ServiceManager.Store
    @loading_spinner = $(@element).find('[data-loading-spinner]')
    @_init_existing_services()
    @_init_available_services()
    @_init_searcher()
    @_listen_for_remove_service_links()
    @hide_loading_spinner()
    
  _init_existing_services: ->
    # find the fieldsets generated by simple_fields_for and replace them with our
    # service_row template
    $existing_service_elements = $(@element).find '[data-existing-service]'
    $existing_service_elements.each (i, element) =>
      service = App.ServiceManager.Service.from_element(element)
      service_row = new App.ServiceManager.ServiceRow service, controller: @, index: i
      $(element).remove()
      @select_service service

  _init_available_services: ->
    @available_services = new App.ServiceManager.Store
    $(@element).find('[data-available-service]')
      .each (_i, element) =>
        @available_services.add App.ServiceManager.Service.from_element element


  _init_searcher: ->
    @searcher = new App.ServiceManager.Searcher $(@element).find('[data-service-searcher]')[0], controller: @
  
  _listen_for_remove_service_links: ->
    $(@element).on 'click', '[data-remove-service-link]', (evt) =>
      evt.preventDefault()
      service_id_str = $(evt.target).attr('data-remove-service-link')
      # evt.target might be the inner content of the link
      unless !!service_id_str
        link = $(evt.target).parents('[data-remove-service-link]')
        service_id_str = $(link).attr('data-remove-service-link')

      service_id = Number service_id_str
      service = @selected_services.find service_id
      @remove_service service
  
  select_service: (service) ->
    service_row = new App.ServiceManager.ServiceRow service,
      controller: @
    $(@element).find('[data-selected-services]').append service_row.to_html()
    @selected_services.add service
    
  remove_service: (service) ->
    @show_loading_spinner =>
      service_row_element = $(@element).find("[data-service-row][data-service-id=\"#{service.id}\"]")
      $(service_row_element).remove()
      @available_services.add service
      @searcher.reset()
      @hide_loading_spinner()
    
  hide_loading_spinner: (callback) ->
    $(@loading_spinner).hide callback
    
  show_loading_spinner: (callback) ->
    $(@loading_spinner).show callback